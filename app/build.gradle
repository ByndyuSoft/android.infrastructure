apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.byndyusoft.androidinfrastructure"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        jackOptions {
            enabled false
        }
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            buildConfigField "String", "APP_NAME", '"Infrastructure"'
            buildConfigField "String", "API_URL", '"http://example.url/"'
        }
        stage {
            buildConfigField "String", "APP_NAME", '"Infrastructure"'
            buildConfigField "String", "API_URL", '"http://example.url/"'
        }
        prod {
            buildConfigField "String", "APP_NAME", '"Infrastructure"'
            buildConfigField "String", "API_URL", '"https://example.url/"'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/license.txt'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

buildscript {
    repositories {
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
        jcenter()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    fileTree(dir: 'libs', include: '**/*.aar').each { File file ->
        dependencies.add("compile", [name: file.name.lastIndexOf('.').with {
            it != -1 ? file.name[0..<it] : file.name
        }, ext                           : 'aar'])
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'

    compile(
            'com.android.support:appcompat-v7:24.2.1',
            'com.android.support:recyclerview-v7:24.2.1',
            'com.android.support:cardview-v7:24.2.1',
            'com.android.support:support-annotations:24.2.1',
            'com.android.support:design:24.2.1'
    )

    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha8'

    compile(
            'com.squareup.okhttp3:okhttp:3.2.0',
            'com.squareup.okhttp3:okhttp-urlconnection:3.2.0',
            'com.squareup.okhttp3:logging-interceptor:3.2.0',

            'com.squareup.retrofit2:retrofit:2.0.1',
            'com.squareup.retrofit2:converter-gson:2.0.1',
            'com.squareup.retrofit2:adapter-rxjava:2.0.1',

            'com.squareup.picasso:picasso:2.5.2',
            'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2',
    )

    // RX
    compile(
            'io.reactivex:rxandroid:1.0.1',
            'io.reactivex:rxjava:1.0.14',
            'com.trello:rxlifecycle:0.4.0',
            'com.trello:rxlifecycle-components:0.4.0',
    )

    // Dagger
    apt 'com.google.guava:guava:19.0'
    compile 'com.google.dagger:dagger:2.4'
    apt 'com.google.dagger:dagger-compiler:2.4'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'javax.inject:javax.inject:1'

    // UI libraries
    compile 'com.afollestad.material-dialogs:core:0.8.5.9'
    compile 'com.github.johnkil.android-robototextview:robototextview:2.5.0'
    compile 'com.joooonho:selectableroundedimageview:1.0.1'
}

android.applicationVariants.all{ variant ->
    variant.mergeAssets.doFirst {
        File fonts = file("${projectDir}/build/intermediates/exploded-aar/com.github.johnkil.android-robototextview/robototextview/2.5.0/assets/fonts")
        if (fonts.exists()) {

            for (File file : fonts.listFiles()) {
                if (file.getName().contains("RobotoSlab") || file.getName().contains("RobotoMono")) {
                    println("delete " + file.getName() + " font")
                    file.delete()
                };
            }
        }
    }
}
