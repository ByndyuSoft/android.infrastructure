apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations.all {
        //http://stackoverflow.com/questions/37347326/errorconflict-with-dependency-com-google-code-findbugsjsr305
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }

    defaultConfig {
        applicationId "com.byndyusoft.androidinfrastructure"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        jackOptions {
            enabled false
        }
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            buildConfigField "String", "APP_NAME", '"Infrastructure"'
            buildConfigField "String", "API_URL", '"http://example.url/"'
        }
        stage {
            buildConfigField "String", "APP_NAME", '"Infrastructure"'
            buildConfigField "String", "API_URL", '"http://example.url/"'
        }
        prod {
            buildConfigField "String", "APP_NAME", '"Infrastructure"'
            buildConfigField "String", "API_URL", '"https://example.url/"'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/rxjava.properties'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

buildscript {
    repositories {
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
        jcenter()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    fileTree(dir: 'libs', include: '**/*.aar').each { File file ->
        dependencies.add("compile", [name: file.name.lastIndexOf('.').with {
            it != -1 ? file.name[0..<it] : file.name
        }, ext                           : 'aar'])
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'

    //Support
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:support-vector-drawable:$supportLibVersion"

    //ConstraintLayout
    compile "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    compile "com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    // RX
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.1.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'

    // Dagger
    annotationProcessor 'com.google.guava:guava:22.0'
    compile 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'javax.inject:javax.inject:1'

    // UI libraries
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'
    compile 'com.github.johnkil.android-robototextview:robototextview:3.0.0'
    compile 'com.joooonho:selectableroundedimageview:1.0.1'
}

android.applicationVariants.all{ variant ->
    variant.mergeAssets.doFirst {
        File fonts = file("${projectDir}/build/intermediates/exploded-aar/com.github.johnkil.android-robototextview/robototextview/2.5.1/assets/fonts")
        if (fonts.exists()) {

            for (File file : fonts.listFiles()) {
                if (file.getName().contains("RobotoSlab") || file.getName().contains("RobotoMono")) {
                    println("delete " + file.getName() + " font")
                    file.delete()
                };
            }
        }
    }
}
